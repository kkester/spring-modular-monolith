plugins {
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
}

group = 'mart.mono'
version = '1.0.0'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.0")
}

dependencies {
    implementation(project(':inventory-policy'))
    implementation(project(':inventory-api-adapter'))
    implementation(project(':inventory-db-adapter'))
    implementation(project(':inventory-event-adapter'))
    implementation(project(':commerce-policy'))
    implementation(project(':commerce-api-adapter'))
    implementation(project(':commerce-event-adapter'))
    implementation(project(':commerce-db-adapter'))
    implementation(project(':inventory-lib'))

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
    implementation 'com.github.loki4j:loki-logback-appender:1.4.1'

    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.postgresql:postgresql'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

tasks.register('installFrontend', Exec) {
    dependsOn cleanFrontend
    inputs.file("frontend/yarn.lock")
    inputs.file("frontend/package.json")
    outputs.dir("frontend/node_modules")

    workingDir "frontend"
    executable "yarn"
    args "install", "--update-checksums"
}

tasks.register('buildFrontend', Exec) {
    dependsOn installFrontend
    inputs.files(file("frontend").listFiles((file -> file.name != "out") as FileFilter))
    outputs.dir("frontend/build")

    workingDir "frontend"
    executable "yarn"
    args "build"
}

tasks.register('copyFrontend', Copy) {
    from buildFrontend.outputs
    into "$buildDir/resources/main/static"
    doLast {
        println "copied frontend artifacts to $buildDir/resources/main/static"
    }
}

tasks.register('cleanFrontend', Delete) {
    delete "frontend/out", "frontend/.next", "$buildDir/resources/main/static"
    doLast {
        println "deleted frontend artifacts"
    }
}

resolveMainClassName {
    dependsOn copyFrontend
}

// region configure Spring's build dependency chain
jar {
    dependsOn copyFrontend
}

// endregion
bootJar {
    dependsOn copyFrontend
}

build {
    dependsOn copyFrontend
}

bootRun {
    dependsOn copyFrontend
}

clean {
    dependsOn cleanFrontend
}
